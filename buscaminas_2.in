set(hyper_res).
assign(max_proofs,-1).

% Lenguaje del Problema
%
% Propiedades:
% - filas(x): Hay x filas en la cuadricúla
% - columnas(x): Hay x columnas en la cuadricúla
% - sensorH(x,y,z): En la casilla de coordenadas (x,y) hay un sensor H que detecta z bombas
% - sensorV(x,y,z): En la casilla de coordenadas (x,y) hay un sensor V que detecta z bombas
% - bomba(x,y): Hay una bomba en la casilla de coordenadas (x,y)
% - vacio(x,y): La casilla de coordenas (x,y) no contiene una bomba (pero tal vez un sensor).
%

make_evaluable(_>_, $GT(_,_)).
make_evaluable(_<_, $LT(_,_)).
make_evaluable(_<=_, $LE(_,_)).
make_evaluable(_+_, $SUM(_,_)).
make_evaluable(_-_, $DIFF(_,_)).

list(demodulators).
    pertenece(x,[]) = $F.
    pertenece(x,[y|w]) = $IF($ID(x,y), $T, pertenece(x,w)).
end_of_list.

formula_list(usable).
    % Construcción de listas de bombas posibles

    % En Filas
    all f (filas(f) -> bombasPosiblesEnFilaInit(f,[])).
    all f (bombasPosiblesEnFilaInit(f,[]) & f>1 -> bombasPosiblesEnFilaInit(f-1,[])).
    all f c (bombasPosiblesEnFilaInit(f,[]) & columnas(c) -> bombasPosiblesEnFilaTemp(f,c-1,[c],1)).
    all f c z x y (bombasPosiblesEnFilaInit(f,[]) & columnas(c) & (sensorV(f,c,z) | sensorH(f,c,z) | vacio(f,c) | (columnaCompletada(c,[x|y]) & -pertenece(f,[x|y]))) -> bombasPosiblesEnFilaTemp(f,c-1,[],0)).
    all f c (bombasPosiblesEnFilaTemp(f,c,[],0) & c>0 -> bombasPosiblesEnFilaTemp(f,c-1,[c],1)).
    all f c z x y (bombasPosiblesEnFilaTemp(f,c,[],0) & c>0 & (sensorV(f,c,z) | sensorH(f,c,z) | vacio(f,c) | (columnaCompletada(c,[x|y]) & -pertenece(f,[x|y]))) -> bombasPosiblesEnFilaTemp(f,c-1,[],0)).
    all f c a b n (bombasPosiblesEnFilaTemp(f,c,[a|b],n) & c>0 -> bombasPosiblesEnFilaTemp(f,c-1,[c,a|b],n+1)).
    all f a b c n z x y (bombasPosiblesEnFilaTemp(f,c,[a|b],n) & c>0 & (sensorV(f,c,z) | sensorH(f,c,z) | vacio(f,c) | (columnaCompletada(c,[x|y]) & -pertenece(f,[x|y]))) -> bombasPosiblesEnFilaTemp(f,c-1,[a|b],n)).
    all f a b n (bombasPosiblesEnFilaTemp(f,0,[a|b],n) -> bombasPosiblesEnFila(f,[a|b],n)).

    % En Columnas
    all c (columnas(c) -> bombasPosiblesEnColumnaInit(c,[])).
    all c (bombasPosiblesEnColumnaInit(c,[]) & c>1 -> bombasPosiblesEnColumnaInit(c-1,[])).
    all f c (bombasPosiblesEnColumnaInit(c,[]) & filas(f) -> bombasPosiblesEnColumnaTemp(c,f-1,[f],1)).
    all f c z x y (bombasPosiblesEnColumnaInit(c,[]) & filas(f) & (sensorV(f,c,z) | sensorH(f,c,z) | vacio(f,c) | (filaCompletada(f,[x|y]) & -pertenece(c,[x|y]))) -> bombasPosiblesEnColumnaTemp(c,f-1,[],0)).
    all f c (bombasPosiblesEnColumnaTemp(c,f,[],0) & f>0 -> bombasPosiblesEnColumnaTemp(c,f-1,[f],1)).
    all f c z x y (bombasPosiblesEnColumnaTemp(c,f,[],0) & f>0 & (sensorV(f,c,z) | sensorH(f,c,z) | vacio(f,c) | (filaCompletada(f,[x|y]) & -pertenece(c,[x|y]))) -> bombasPosiblesEnColumnaTemp(c,f-1,[],0)).
    all f a b c n (bombasPosiblesEnColumnaTemp(c,f,[a|b],n) & f>0 -> bombasPosiblesEnColumnaTemp(c,f-1,[f,a|b],n+1)).
    all f a b c n z x y (bombasPosiblesEnColumnaTemp(c,f,[a|b],n) & f>0 & (sensorV(f,c,z) | sensorH(f,c,z) | vacio(f,c) | (filaCompletada(f,[x|y]) & -pertenece(c,[x|y]))) -> bombasPosiblesEnColumnaTemp(c,f-1,[a|b],n)).
    all c a b n (bombasPosiblesEnColumnaTemp(c,0,[a|b],n) -> bombasPosiblesEnColumna(c,[a|b],n)).

    % Sensores que no detectan nada - Vertical
    all x y f (filas(f) & sensorV(x,y,0) -> vaciarColumna(y,f)).
    all f c (vaciarColumna(c,f) & f > 0 -> vacio(f,c) & vaciarColumna(c,f-1)).

    % Sensores que no detectan nada - Horizontal
    all x y c (columnas(c) & sensorH(x,y,0) -> vaciarFila(x,c)).
    all f c (vaciarFila(f,c) & c > 0 -> vacio(f,c) & vaciarFila(f,c-1)).

    % Sensores que detectan todo - Vertical
    all a b c f n (sensorV(f,c,n) & bombasPosiblesEnColumna(c,[a|b],n) -> llenarColumna(c,[a|b],[a|b])).
    all a b c d e (llenarColumna(c,[a|b],[d|e]) -> bomba(a,c) & llenarColumna(c,b,[d|e])).
    all a b c (llenarColumna(c,[],[a|b]) -> columnaCompletada(c,[a|b])).

    % Sensores que detectan todo - Horizontal
    all a b c f n (sensorH(f,c,n) & bombasPosiblesEnFila(f,[a|b],n) -> llenarFila(f,[a|b],[a|b])).
    all a b c d f (llenarFila(f,[a|b],[c|d]) -> bomba(f,a) & llenarFila(f,b,[c|d])).
    all a b f (llenarFila(f,[],[a|b]) -> filaCompletada(f,[a|b])).

    % Contar bombas
    all f (filas(f) -> bombasEnFila(f,[],0)).
    all f (bombasEnFila(f,[],0) & f > 1 -> bombasEnFila(f-1,[],0)).
    all x y (bomba(x,y) & bombasEnFila(x,[],0) -> bombasEnFila(x,[y],1)).
    all x y a b n (bomba(x,y) & bombasEnFila(x,[a|b],n) & -pertenece(y,[a|b]) -> bombasEnFila(x,[y,a|b],n+1)).

    all c (columnas(c) -> bombasEnColumna(c,[],0)).
    all c (bombasEnColumna(c,[],0) & c>1 -> bombasEnColumna(c-1,[],0)).
    all x y (bomba(x,y) & bombasEnColumna(y,[],0) -> bombasEnColumna(y,[x],1)).
    all x y a b n (bomba(x,y) & bombasEnColumna(y,[a|b],n) & -pertenece(x,[a|b]) -> bombasEnColumna(y,[x,a|b],n+1)).

    all f c z a b (sensorV(f,c,z) & bombasEnColumna(c,[a|b],z) -> columnaCompletada(c,[a|b])).
    all f c z a b (sensorH(f,c,z) & bombasEnFila(f,[a|b],z) -> filaCompletada(f,[a|b])).
end_of_list.

formula_list(sos).
    % Situación inicial.
    filas(5).
    columnas(5).
    sensorV(3,2,0).
    sensorH(1,3,2).
    sensorV(4,3,3).
    sensorH(2,2,1).
    sensorV(5,5,2).
    sensorH(4,1,0).
    sensorV(1,1,2).
end_of_list.

formula_list(passive).
    % Cada vez que encontramos a una bomba, la devolvemos como respuesta.
    all x y (bomba(x,y) -> $ANS(bomba(x,y))).
end_of_list.
